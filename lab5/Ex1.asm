
CSEG AT 0000H
JMP MAIN

CSEG AT 0023H ; ADDRESS OF SERIAL INTERUPT
JMP SERIAL ; WE JMP TO OTHER LOCATION BECUASE OF SHORT MEMORY

CSEG AT 0100H ; MAIN

MAIN:
	CLR SM0  ;TURN OFF BIT 7 IN SCON
	SETB SM1 ;TURN ON BIT 6 IN SCON
	SETB REN ;TURN ON BIT 4 SCON
	ANL PCON , #07FH ; SMOD=0 FOR BAUD RATE CALCULATION
	ANL TMOD , #02FH ; TURNNING OFF BIT 4 TO SET TIMER1 TO AUTO RELOAD MODE
	ORL TMOD , #020H ;  TURNNING ON BIT 5 TO SET TIMER1 TO AUTO RELOAD MODE
	MOV TH1 , #00EEH ; OVERFLOW EVERY 18 CYCLE 256-238=18 --> 614,400 [OVERFLOWS/SEC]
				    ; 614,400 *2^0/32 = BAUD RATE OF 19200[BITS/SEC]
	SETB TR1 ; TURN TIMER 1 ON
	;-----SET INTERUPTS-----
	CLR ET1 ; CLEAR INTERUPT FOR TIMER1
	SETB ES ; ENABLE SERIAL PORT INTERUPT
	SETB EA ; GLOBAL INTERRUPT ENABLE
	JMP $ ; inf loop

SERIAL:
	CLR TI ; CLEAR THE TRANSMIT FLAG OF THE UART
	JBC RI,RI_ON ;TURN OFF RI AND JMP TO SEND IT BACK
	;TRANSMINT INTERUPT
	PUSH ACC
	CLR A
	MOVC A , @A+DPTR ; DPTR HOLD THE ADDRESS TO THE LABEL THAT REPRESNT THE LETTER THAT SUPPOSED TO BE PRINT
	JZ IN_NULL ; DO NOT PRINT OF THE LETTER THAT A HOLDS IS NULL
	MOV SBUF , A ; SEND A TO SBUF WILL PRINT IT
	INC DPTR ; FOR READING THE NEXT LETTER IN THE STRING
IN_NULL:
	POP ACC
RETI

RI_ON:
		CLR RI ;CLEAR THE RECIVE FLAG OF THE UART
		MOV R2 , SBUF ; REGISTER R2 WILL GET THE LETTER THAT ENTERED THE SBUF FROM THE
		CJNE R2 , #'a' ,CHECK_B
		MOV DPTR , #A_PRESSED
		JMP END_RI
	CHECK_B:
		CJNE R2 , #'b' , CHECK_C
		MOV DPTR , #B_PRESSED
		JMP END_RI
	CHECK_C:
		CJNE R2 , #'c' , CHECK_D
		MOV DPTR , #C_PRESSED
		JMP END_RI
	CHECK_D:
		CJNE R2 , #'d' , END_RI
		MOV DPTR , #D_PRESSED
	END_RI:
		SETB TI
RETI

CSEG AT 0200H
A_PRESSED: DB 'Alpha'
		   DB 13 ; CARRIAGE RETURN
		   DB 10 ; NEW LINE
		   DB 0 ; NULL

B_PRESSED: DB 'Beta'
		   DB 13 ; CARRIAGE RETURN
		   DB 10 ; NEW LINE
		   DB 0 ; NULL

C_PRESSED: DB 'Charlie'
		   DB 13 ; CARRIAGE RETURN
		   DB 10 ; NEW LINE
		   DB 0 ; NULL

D_PRESSED: DB 'Delta'
		   DB 13 ; CARRIAGE RETURN
		   DB 10 ; NEW LINE
		   DB 0 ; NULL
END
